import org.gradle.nativeplatform.platform.internal.DefaultNativePlatform

plugins {
    id 'java'
    id 'idea'
    id 'eclipse'
    id 'application'
    id 'org.beryx.jlink' version '2.24.4'
}

group 'com.codedead'
version '1.0.1'

def currentOS = DefaultNativePlatform.currentOperatingSystem

java {
    targetCompatibility = JavaVersion.VERSION_18
    sourceCompatibility = JavaVersion.VERSION_18
}

application {
    mainModule = 'Opal'
    mainClass = 'com.codedead.opal.OpalApplication'
}

jlink {
    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
    forceMerge('log4j-api', 'jackson')
    launcher {
        name = 'Opal'
    }

    if (currentOS.isWindows()) {
        jpackage {
            installerType = 'msi'
            icon = "${project.rootDir}/.msi/opal.ico"
            installerOptions = [
                    '--win-menu',
                    '--win-menu-group', 'CodeDead',
                    '--win-shortcut',
                    '--win-dir-chooser',
                    '--copyright', 'Copyright (c) 2022 CodeDead',
                    '--description', 'Opal is a free and open-source JavaFX application that can play relaxing music in the background',
                    '--vendor', 'CodeDead',
                    '--license-file', 'LICENSE'
            ]
        }
    } else if (currentOS.isLinux()) {
        jpackage {
            installerType = 'rpm'
            icon = "${project.rootDir}/src/main/resources/images/opal.png"
            installerOptions = [
                    '--linux-shortcut',
                    '--linux-package-name', 'opal-codedead',
                    '--linux-rpm-license-type', 'GPLv3',
                    '--copyright', 'Copyright (c) 2022 CodeDead',
                    '--description', 'Opal is a free and open-source JavaFX application that can play relaxing music in the background',
                    '--vendor', 'CodeDead',
                    '--license-file', 'LICENSE'
            ]
        }
    }
}

task AppImage() {
    dependsOn jpackageImage
    doLast {
        copy {
            from '.AppImage/.AppDir'
            into "${buildDir}/AppImage/Opal.AppDir"
        }
        copy {
            from "${buildDir}/jpackage/Opal/"
            into "${buildDir}/AppImage/Opal.AppDir/usr/"
        }
        copy {
            from '.AppImage/.AppDir/Opal.png'
            into "${buildDir}/AppImage/Opal.AppDir/usr/lib"
        }

        exec {
            commandLine 'sh', "${project.rootDir}/.AppImage/createAppImage.sh", "${project.version.toString()}"
        }

        delete "${buildDir}/AppImage/Opal.AppDir"
    }
}

configure(AppImage) {
    group = 'build'
    description = 'Create an AppImage after creating the image of the application'
}

clean.doFirst {
    delete 'default.properties'
    delete 'license.pdf'
    delete 'help.pdf'
    delete 'logs'
}

repositories {
    mavenCentral()
}

def platform
if (currentOS.isWindows()) {
    platform = 'win'
} else if (currentOS.isLinux()) {
    platform = 'linux'
} else if (currentOS.isMacOsX()) {
    platform = 'mac'
}

dependencies {
    implementation "org.openjfx:javafx-base:18:${platform}"
    implementation "org.openjfx:javafx-controls:18:${platform}"
    implementation "org.openjfx:javafx-graphics:18:${platform}"
    implementation "org.openjfx:javafx-fxml:18:${platform}"
    implementation "org.openjfx:javafx-media:18:${platform}"
    implementation 'org.apache.logging.log4j:log4j-core:2.17.2'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.13.2.2'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.2'
}

test {
    useJUnitPlatform()
}
