import org.gradle.nativeplatform.platform.internal.DefaultNativePlatform

plugins {
    id 'java'
    id 'idea'
    id 'eclipse'
    id 'application'
    id 'org.beryx.jlink' version '2.24.0'
}

group 'com.codedead'
version '1.0-SNAPSHOT'

java {
    targetCompatibility = JavaVersion.VERSION_16
    sourceCompatibility = JavaVersion.VERSION_16
}

application {
    mainModule = 'Opal'
    mainClass = 'com.codedead.opal.OpalApplication'
}

jlink {
    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
    forceMerge('log4j-api', 'jackson')
    launcher {
        name = 'Opal'
    }
}

clean.doFirst {
    delete 'default.properties'
    delete 'license.pdf'
    delete 'logs'
}

repositories {
    mavenCentral()
}

def currentOS = DefaultNativePlatform.currentOperatingSystem
def platform
if (currentOS.isWindows()) {
    platform = 'win'
} else if (currentOS.isLinux()) {
    platform = 'linux'
} else if (currentOS.isMacOsX()) {
    platform = 'mac'
}

dependencies {
    implementation "org.openjfx:javafx-base:16:${platform}"
    implementation "org.openjfx:javafx-controls:16:${platform}"
    implementation "org.openjfx:javafx-graphics:16:${platform}"
    implementation "org.openjfx:javafx-fxml:16:${platform}"
    implementation "org.openjfx:javafx-media:16:${platform}"
    implementation "org.apache.logging.log4j:log4j-core:2.14.1"
    implementation "com.fasterxml.jackson.core:jackson-databind:2.12.3"
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'
}

test {
    useJUnitPlatform()
}
